10 D$=CHR$(4)
11 PRINT D$;"PR#3" : REM 80-column mode
13 GR : HOME       : GR
20 LET L = 1               : REM SNAKE STARTS WITH LENGTH 1
30 LET W = 39: LET H = 31: REM WIDTH AND HEIGHT
40 GOSUB 2000              : REM INITIALIZE FOOD
50 DIM X(100) : DIM Y(100) : REM INTIALIZE ARRAY
60 X(L) = 20 : Y(L) = 24 

65 REM DRAW LOOP
70 GOSUB 3000
80 FOR N = 1 TO 1000 : NEXT N
90 GOTO 70

999  REM READ KEYBOARD INPUT
1000 LET K = PEEK(49152)
1010 IF K = 196 THEN X(L) = X(L) + 1
1020 IF K = 193 THEN X(L) = X(L) - 1
1030 IF K = 215 THEN Y(L) = Y(L) - 1
1040 IF K = 211 THEN Y(L) = Y(L) + 1
1050 IF X(L) < 1 OR X(L) > W THEN GOTO 6000
1060 IF Y(L) < 1 OR Y(L) > H THEN GOTO 6000
1070 IF L > 1 THEN GOSUB 7000
1080 RETURN

1990 REM CREATE FOOD
2000 LET FX = INT(RND(1) * W)
2010 LET FY = INT(RND(1) * H)
2020 COLOR = 1
2030 PLOT FX,FY
2040 RETURN

2990 REM DISPLAY SNAKE
3000 COLOR = 0
3010 PLOT X(1),Y(1)
3020 IF L > 1 THEN GOSUB 5000
3030 GOSUB 1000
3040 IF X(L) = FX AND Y(L) = FY THEN GOSUB 4000
3050 FOR I = 1 TO L
3060 COLOR = 15
3070 PLOT X(I),Y(I)
3080 NEXT I
3090 RETURN

3990 REM GROW SNAKE
4000 L = L + 1
4010 X(L) = X(L - 1)
4020 Y(L) = Y(L - 1)
4030 GOSUB 2000
4040 RETURN

4990 REM SHIFT SNAKE ARRAY
5000 FOR I = 1 TO L - 1
5010 X(I) = X(I + 1)
5020 Y(I) = Y(I + 1)
5030 NEXT I
5040 RETURN

6000 REM GAME OVER
6010 HOME
6020 PRINT "GAME OVER!"
6030 END

7000 REM CHECK IF SNAKE HIT ITSELF
7010 FOR I = 1 TO L-1
7020 IF X(L) = X(I) AND Y(L) = Y(I) GOTO 6000
7030 NEXT I
7040 RETURN
